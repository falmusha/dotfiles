" vim or neovim?
let g:nvim = has('nvim')

"-------------------------------------------------------------------------------
"--------------------------[ vanilla customization ]----------------------------
"-------------------------------------------------------------------------------

" looks ------------------------------------------------------------------------

set termguicolors

" set Vim-specific sequences for RGB colors when using tmux
if !g:nvim && $TERM == "screen-256color"
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
end

syntax on

filetype plugin on " enable file type detection

" options ----------------------------------------------------------------------

set autoread " automatically read files when changed
set colorcolumn=+1 " highlight lines over that 1 column after 'textwidth'
set expandtab " spaces instead of tabs
set list listchars=tab:»·,precedes:-,trail:·,nbsp:+
set mouse=a
set noswapfile " remove swap files
set number relativenumber " show line numbers
set shiftwidth=2 | set tabstop=2 " 1 tab = 2 spaces
set splitbelow " open new split panes to bottom
set splitright " open new split panes to right
set statusline=[%n]%f%m%r%w%=[%{fugitive#head()}]%y[%p%%][%l\/%L,%v]
set textwidth=80
set wildignore+=*/tmp/*,*.so,*.swp,*.zip,*.class,*DS_Store* " general
set wildignore+=*bower_components/**,*node_modules/**,*build/**,*dist/** " JS
set wildignore+=*deps/**,*_build/** " elixir

"-------------------------------------------------------------------------------
" key mappings
"-------------------------------------------------------------------------------

let mapleader = " "
let maplocalleader = ","

" normal -----------------------------------------------------------------------

" move between splits with CTRL + hjkl
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-h> <C-w>h
nnoremap <C-l> <C-w>l

" move up and down a wrapped line
nnoremap j gj
nnoremap k gk

" increase height by 1 columns
nnoremap <silent> <down> :resize -1<cr>

" decrease height by 1 columns
nnoremap <silent> <up> :resize +1<cr>

" increase width by 1 columns
nnoremap <silent> <right> :vertical resize +1<cr>

" decrease width by 1 columns
nnoremap <silent> <left>  :vertical resize -1<cr>

" repeat last commnad
nnoremap <leader>lc @:

" got to last buffer
nnoremap <leader>lb :b#<cr>

" remove trailing white spaces
nnoremap <leader>s :call StripTrailingWhitespace()<cr>

" quick fuzzy-ish edit
nnoremap <leader>e :edit **/

" disable highlights after search
nnoremap <leader>q :nohlsearch<cr>

" open vimrc file to edit it
nnoremap <leader>ev :vsplit $MYVIMRC<cr>

" reload vimrc file
nnoremap <leader>sv :source $MYVIMRC<cr>

" use - and | for horizontal or vertical splits
nnoremap \| :vsplit<cr>
nnoremap - :split<cr>

" qq to record, Q to replay
nnoremap Q @q

" enable spelling
nnoremap <C-s> :setlocal spell! spelllang=en_us<cr>

" increasing/decreasing numbers
nnoremap + <C-a>
nnoremap _ <C-x>

" focus mode
nnoremap <leader>F :call FocusMode()<cr>

" insert -----------------------------------------------------------------------

" delete line when CTRL+d and enter insert mode at end of prev line
inoremap <C-d> <esc>ddA

" disable ESC in insert mode
inoremap <esc> <nop>

" leave insert mode
inoremap jj <esc>

" disable arrow keys in insert mode
inoremap <up> <nop>
inoremap <down> <nop>
inoremap <left> <nop>
inoremap <right> <nop>

"-------------------------------------------------------------------------------
" helper functions
"-------------------------------------------------------------------------------

function! StripTrailingWhitespace()
  if !&binary && &filetype != 'diff'
    normal mz
    normal Hmy
    %s/\s\+$//e
    normal 'yz<cr>
    normal `z
  endif
endfunction

function! FocusMode()
  setlocal textwidth=120
  setlocal spell spelllang=en_ca
  :Goyo 121
endfunction

function! s:sourceLocalConfigs()
  " source local machine specific vimrc
  if filereadable(glob('~/.vimrc.local'))
    source ~/.vimrc.local
  endif
endfunction

"-------------------------------------------------------------------------------
" auto commands
"-------------------------------------------------------------------------------

" FileType specific ------------------------------------------------------------

augroup filetype_elixir
  autocmd!
  autocmd FileType elixir,eelixir setlocal textwidth=98
augroup END

augroup filetype_ruby
  autocmd!
  autocmd FileType ruby,eruby setlocal textwidth=98
augroup END

augroup filetype_python
  autocmd!
  autocmd FileType python setlocal textwidth=88
augroup END

augroup filetype_markdown
  autocmd!
  autocmd FileType markdown setlocal textwidth=120
augroup END

"-------------------------------------------------------------------------------
"--------------------------[ plugins & customization ]--------------------------
"-------------------------------------------------------------------------------

if g:nvim
  let s:vim_plug = '~/.local/share/nvim/site/autoload/plug.vim'
  let s:vim_plugins_dir = '~/.local/share/nvim/plugged'
else
  let s:vim_plug = '~/.vim/autoload/plug.vim'
  let s:vim_plugins_dir = '~/.vim/plugged'
endif

let g:plugged = filereadable(glob(s:vim_plug))

if !g:plugged
  call s:sourceLocalConfigs()
  finish
end

call plug#begin(s:vim_plugins_dir)

Plug 'airblade/vim-gitgutter'
Plug 'blueyed/vim-diminactive'
Plug 'chriskempson/base16-vim'
Plug 'christoomey/vim-tmux-navigator'
Plug 'dense-analysis/ale'
Plug 'elixir-editors/vim-elixir'
Plug 'junegunn/goyo.vim'
Plug 'junegunn/limelight.vim'
Plug 'prettier/vim-prettier', { 'do': 'npm install' }
Plug 'sbdchd/neoformat'
Plug 'slashmili/alchemist.vim'
Plug 'tmux-plugins/vim-tmux-focus-events'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-vinegar'
Plug 'tweekmonster/startuptime.vim', { 'on':  'StartupTime' }
Plug 'wellle/tmux-complete.vim'

if !g:nvim
  " vim specific plugins
  Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
  Plug 'junegunn/fzf.vim'
else
  " neovim specific plugins
  Plug 'neovim/nvim-lspconfig'
  Plug 'nvim-lua/plenary.nvim'
  Plug 'nvim-telescope/telescope.nvim'
  Plug 'nvim-treesitter/nvim-treesitter', { 'do': ':TSUpdate' }
endif

call plug#end()

" base16-vim -------------------------------------------------------------------
let base16colorspace=256 " Access colors present in 256 colorspace
colorscheme base16-default-dark

" limelight.vim ----------------------------------------------------------------
autocmd! User GoyoEnter Limelight
autocmd! User GoyoLeave Limelight!

" ale --------------------------------------------------------------------------
let g:ale_linters = {
\ 'python': []
\}

" alchemist.vim ----------------------------------------------------------------
let g:alchemist_tag_map = '<C-]>'
let g:alchemist_tag_stack_map = '<C-[>'

" vim-diminactive --------------------------------------------------------------
let g:diminactive_enable_focus = 1
let g:diminactive_use_syntax = 1

" neoformat --------------------------------------------------------------------
let g:neoformat_enabled_python = ['black']

" format current buffer using Neoformat
nnoremap <leader>f <cmd>Neoformat<cr>

"-------------------------------------------------------------------------------
" vim plugins customization
"-------------------------------------------------------------------------------
if !g:nvim

" fzf.vim ----------------------------------------------------------------------
let g:fzf_layout = { 'down': '~20%' }

" customize fzf colors to match color scheme
let g:fzf_colors =
\ { 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'PreProc'],
  \ 'border':  ['fg', 'Ignore'],
  \ 'prompt':  ['fg', 'Conditional'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment'] }

" [Buffers] Jump to the existing window if possible
let g:fzf_buffers_jump = 1

" use ripgrep instead of ag:
command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \   'rg --column --line-number --no-heading --color=always '.shellescape(<q-args>), 1,
  \   <bang>0 ? fzf#vim#with_preview('up:80%')
  \           : fzf#vim#with_preview('right:50%'),
  \   <bang>0)

" preview :Files
command! -bang -nargs=? -complete=dir Files
  \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)

" open FZF fuzzy file finder
nnoremap <C-p> <cmd>Files<cr>

" start rgrep find command
nnoremap <leader>g <cmd>Rg<space>

" rgrep on current word
nnoremap <leader>G :execute 'Rg ' expand('<cword>')<cr>

"-------------------------------------------------------------------------------
" neovim plugins customization
"-------------------------------------------------------------------------------
else

" nvim-lspconfig ---------------------------------------------------------------
lua << EOF
  local lsp = require('lspconfig')
  -- python
  lsp.pyright.setup({})
  -- vimscript
  lsp.vimls.setup({})
EOF

" telescope --------------------------------------------------------------------
lua << EOF
require('telescope').setup({
  defaults = {
    layout_config = { preview_width = 0.6 }
  }
})
EOF

" open Telescope fuzzy file finder
nnoremap <c-p> <cmd>Telescope find_files<cr>

" start Telescope live grep
nnoremap <leader>g <cmd>Telescope live_grep<cr>

" grep string under cursor
nnoremap <leader>G <cmd>Telescope grep_string<cr>

endif " end vim/neovim custom plugin customization

call s:sourceLocalConfigs()
